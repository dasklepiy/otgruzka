
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Дата = ТекущаяДата();
	ЗаполнитьТаблицуКлиентов();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуКлиентов();
	РаспределениеТоваров.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.НомерМестаОтгрузки КАК МестоОтгрузки,
	|	ТоварыНаСкладахОстатки.Контрагент КАК Контрагент,
	|	ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоКоробокОстаток, 0) - ЕСТЬNULL(ПутевыеЛистыОстатки.КоличествоКоробокОстаток, 0) КАК КоличествоКоробок,
	|	ЕСТЬNULL(ТоварыНаСкладахОстатки.ВесОстаток, 0) - ЕСТЬNULL(ПутевыеЛистыОстатки.ВесОстаток, 0) КАК Вес,
	|	ЕСТЬNULL(ТоварыНаСкладахОстатки.ОбъемОстаток, 0) - ЕСТЬNULL(ПутевыеЛистыОстатки.ОбъемОстаток, 0) КАК Объем,
	|	ТоварыНаСкладахОстатки.ДокументПрихода
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки КАК ТоварыНаСкладахОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПутевыеЛисты.Остатки КАК ПутевыеЛистыОстатки
	|		ПО ТоварыНаСкладахОстатки.НомерМестаОтгрузки = ПутевыеЛистыОстатки.МестоОтгрузки
	|			И ТоварыНаСкладахОстатки.Контрагент = ПутевыеЛистыОстатки.Контрагент
	|			И ТоварыНаСкладахОстатки.ДокументПрихода = ПутевыеЛистыОстатки.ДокументПрихода
	|ГДЕ
	|	ТоварыНаСкладахОстатки.ДокументПрихода.Город = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.КоличествоКоробок > 0 Тогда
			НоваяСтрока = РаспределениеТоваров.Добавить();
			НоваяСтрока.Контрагент 			= Выборка.Контрагент;
			НоваяСтрока.МестоОтгрузки		= Выборка.МестоОтгрузки;
			НоваяСтрока.КоличествоКоробок	= Выборка.КоличествоКоробок;
			НоваяСтрока.Объем				= Выборка.Объем;
			НоваяСтрока.Вес					= Выборка.Вес;
			НоваяСтрока.ДокументПрихода		= Выборка.ДокументПрихода;
			НоваяСтрока.Комментарий			= Выборка.ДокументПрихода.Комментарий;
		КонецЕсли;
	КонецЦикла;
	
	
КонецПроцедуры


&НаКлиенте
Процедура СоздатьПутевыеЛисты(Команда)
	СоздатьПутевыеЛистыНаСервере();
КонецПроцедуры

&НаСервере
Процедура СоздатьПутевыеЛистыНаСервере()
	
	ТаблицаВодителей = РаспределениеТоваров.Выгрузить();
	ТаблицаВодителей.Свернуть("Водитель");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПутевойЛист.Ссылка,
	|	ПутевойЛист.Водитель
	|ИЗ
	|	Документ.ПутевойЛист КАК ПутевойЛист
	|ГДЕ
	|	ПутевойЛист.Дата = &Дата";
	Запрос.УстановитьПараметр("Дата", Дата);
	ТаблицаПутевыхЛистов = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ТекСтрока Из ТаблицаВодителей Цикл
		НайденныеСтроки = РаспределениеТоваров.НайтиСтроки(Новый Структура("Водитель", ТекСтрока.Водитель));	
		Если Не НайденныеСтроки.Количество() = 0 Тогда
			НайденныеПутевыеЛисты = ТаблицаПутевыхЛистов.НайтиСтроки(Новый Структура("Водитель", ТекСтрока.Водитель));
			Если НайденныеПутевыеЛисты.Количество() = 0 Тогда
				НовыйДокумент = Документы.ПутевойЛист.СоздатьДокумент();
				НовыйДокумент.Водитель		= ТекСтрока.Водитель;
				НовыйДокумент.Автомобиль	= ТекСтрока.Водитель.Автомобиль;
				НовыйДокумент.Дата 			= Дата;
			Иначе
				НовыйДокумент = НайденныеПутевыеЛисты[0].Ссылка.ПолучитьОбъект();	
			КонецЕсли;
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				НоваяСтрока = НовыйДокумент.Данные.Добавить();
				НоваяСтрока.Контрагент			= НайденнаяСтрока.Контрагент;
				НоваяСтрока.ДокументПрихода 	= НайденнаяСтрока.ДокументПрихода;
				НоваяСтрока.МестоОтгрузки		= НайденнаяСтрока.МестоОтгрузки;
				НоваяСтрока.КоличествоКоробок	= НайденнаяСтрока.КоличествоКоробок;
				НоваяСтрока.Вес					= НайденнаяСтрока.Вес;
				НоваяСтрока.Объем				= НайденнаяСтрока.Объем;
			КонецЦикла;
			Попытка
				НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
				Сообщить("Создан путевой лист");
			Исключение
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
	ЗаполнитьТаблицуКлиентов();
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеТоваровВодительПриИзменении(Элемент)
	ТекущиеДанные = Элементы.РаспределениеТоваров.ТекущиеДанные;
	
	НайденныеКонтрагенты = РаспределениеТоваров.НайтиСтроки(Новый Структура("Контрагент", ТекущиеДанные.Контрагент));
	Если Не НайденныеКонтрагенты.Количество() = 0 Тогда
		Для Каждого ТекСтрока Из НайденныеКонтрагенты Цикл
			ТекСтрока.Водитель = ТекущиеДанные.Водитель;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПутевыеЛисты(Команда)
	ЗагрузитьПутевыеЛистыНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПутевыеЛистыНаСервере()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПутевыеЛистыОстатки.Водитель,
	|	ПутевыеЛистыОстатки.МестоОтгрузки,
	|	ПутевыеЛистыОстатки.Контрагент КАК Контрагент,
	|	ПутевыеЛистыОстатки.ДокументПрихода,
	|	ПутевыеЛистыОстатки.ВесОстаток КАК Вес,
	|	ПутевыеЛистыОстатки.КоличествоКоробокОстаток КАК КоличествоКоробок,
	|	ПутевыеЛистыОстатки.ОбъемОстаток КАК Объем,
	|	ПутевыеЛистыОстатки.ПутевойЛист
	|ИЗ
	|	РегистрНакопления.ПутевыеЛисты.Остатки КАК ПутевыеЛистыОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент";
	РаспределениеТоваров.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

&НаКлиенте
Процедура ОтредактироватьПутевыеЛисты(Команда)
	ОтредактироватьПутевыеЛистыНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОтредактироватьПутевыеЛистыНаСервере()
	ТаблицаКлиентов = РаспределениеТоваров.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПутевыеЛистыОстатки.ПутевойЛист
	|ИЗ
	|	РегистрНакопления.ПутевыеЛисты.Остатки(, ) КАК ПутевыеЛистыОстатки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		мОбъект = Выборка.ПутевойЛист.ПолучитьОбъект();
		НайденныеДанные = ТаблицаКлиентов.НайтиСтроки(Новый Структура("Водитель", Выборка.ПутевойЛист.Водитель));
		Если Не НайденныеДанные.Количество() = 0 Тогда
			мОбъект.Данные.Очистить();
			Для Каждого ТекСтрока Из НайденныеДанные Цикл
				НоваяСтрока = мОбъект.Данные.Добавить();
				НоваяСтрока.Контрагент			= ТекСтрока.Контрагент;
				НоваяСтрока.МестоОтгрузки 		= ТекСтрока.МестоОтгрузки;
				НоваяСтрока.КоличествоКоробок	= ТекСтрока.КоличествоКоробок;
				НоваяСтрока.Вес					= ТекСтрока.Вес;
				НоваяСтрока.Объем				= ТекСтрока.Объем;
				НоваяСтрока.ДокументПрихода		= ТекСтрока.ДокументПрихода;
			КонецЦикла;
			мОбъект.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	ТабличныйДокумент = ПечатьПутевыеЛисты(Дата);
	Если ТабличныйДокумент <> Неопределено Тогда
		ТабличныйДокумент.Показать();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПечатьПутевыеЛисты(Дата);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПутевойЛист.Ссылка
	|ИЗ
	|	Документ.ПутевойЛист КАК ПутевойЛист
	|ГДЕ
	|	ПутевойЛист.Дата МЕЖДУ &ДатаНач И &ДатаКон
	|	И ПутевойЛист.ПометкаУдаления = ЛОЖЬ";
	Запрос.УстановитьПараметр("ДатаНач", НачалоДня(Дата));
	Запрос.УстановитьПараметр("ДатаКон", КонецДня(Дата));
	Выборка = Запрос.Выполнить().Выбрать();
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ЛистПоСчету = 0;
	
	Пока Выборка.Следующий() Цикл
		ЛистПоСчету = ЛистПоСчету + 1;
		
		Макет = Обработки.АРМСтаршийВодитель.ПолучитьМакет("МакетПутевойЛист");
		
		Область = Макет.ПолучитьОбласть("Шапка");
		Область.Параметры.Номер = Выборка.Ссылка.Номер;
		Область.Параметры.Дата	= Формат(Дата, "ДФ=dd.MM.yyyy");
		Область.Параметры.Водитель = Выборка.Ссылка.Водитель;
		Область.Параметры.Автомобиль = Выборка.Ссылка.Автомобиль;
		ТабличныйДокумент.Вывести(Область);
		
		НомерПП = 1;
		
		
		Для Каждого ТекСтрока Из Выборка.Ссылка.Данные Цикл
			Область = Макет.ПолучитьОбласть("Контрагент");
			Область.Параметры.НомерПП			= НомерПП;
			Область.Параметры.Контрагент 		= ТекСтрока.Контрагент;
			Область.Параметры.МестоОтгрузки		= ТекСтрока.МестоОтгрузки;
			Область.Параметры.Вес				= ТекСтрока.Вес;
			Область.Параметры.Объем				= ТекСтрока.Объем;
			Область.Параметры.КоличествоКоробок	= ТекСтрока.КоличествоКоробок;
			Подразделение = ТекСтрока.ДокументПрихода.Организация;
			
			Если Лев(Подразделение, 1) = "А" Тогда
				Область.Параметры.Подразделение = "А";	
			Иначе
				Область.Параметры.Подразделение = "Н";
			КонецЕсли;
			
			КопияДанные = ТекСтрока.ДокументПрихода.ДокументыРасхода.Выгрузить();
			КопияДанные.Свернуть("Комментарий",);
			
			Если КопияДанные.Количество() > 1 Тогда
				сКомментарий = "";
				Для Каждого Комментарий Из ТекСтрока.ДокументПрихода.ДокументыРасхода Цикл
					сКомментарий = сКомментарий + " " + Строка(Комментарий.СчетФ) + " " + Строка(Комментарий.Комментарий) + " ";  		
				КонецЦикла;
				Область.Параметры.Комментарий = СокрЛП(сКомментарий);	
			Иначе
				Область.Параметры.Комментарий = КопияДанные[0].Комментарий;	
			КонецЕсли;
			
			// Направление
				Если ТекСтрока.ДокументПрихода.Город Тогда
					Направление="Город";
				Иначе
					Направление="Область";	
				КонецЕсли;
				Область.Параметры.Направление=Направление;

			
			ТабличныйДокумент.Вывести(Область);
			НомерПП = НомерПП + 1;
		КонецЦикла;
		
		КолСтрок=25; //Кол строк в таблице путевого листа
		
		Если НомерПП < КолСтрок Тогда
			Пока НомерПП < КолСтрок Цикл
				НомерПП = НомерПП + 1;
				Область = Макет.ПолучитьОбласть("Контрагент");
				Область.Параметры.НомерПП	= НомерПП;
				ТабличныйДокумент.Вывести(Область);
			КонецЦикла;
		КонецЕсли;
		
		Область = Макет.ПолучитьОбласть("Подвал");
		Область.Параметры.КоличествоКоробок	= Выборка.Ссылка.Данные.Итог("КоличествоКоробок");
		Область.Параметры.Вес				= Выборка.Ссылка.Данные.Итог("Вес");
		Область.Параметры.Объем				= Выборка.Ссылка.Данные.Итог("Объем");
		ТабличныйДокумент.Вывести(Область);
		
		Если ЛистПоСчету = 2 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			ЛистПоСчету = 0;
		КонецЕсли;
		
	КонецЦикла;
	ТабличныйДокумент.АвтоМасштаб			= Истина;
	ТабличныйДокумент.ОтображатьСетку		= Ложь;
	ТабличныйДокумент.ОтображатьЗаголовки	= Ложь;
	Возврат ТабличныйДокумент;
КонецФункции

&НаКлиенте
Процедура РаспределениеТоваровВодительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка=Ложь;
	ФормаВыбора=ПолучитьФорму("Обработка.АРМСтаршийВодитель.Форма.ФормаВыбораВодителя",,Элемент);
	ФормаВыбора.Открыть();
КонецПроцедуры





